plugins {
    alias(libs.plugins.application)
    id 'org.greenrobot.greendao'
}

apply from: '../common.gradle'

// Android 代码规范文档：https://github.com/getActivity/AndroidCodeStandard
android {
    namespace 'run.yigou.gxzy'

    buildFeatures {
        // 是否生成 BuildConfig 类
        buildConfig true
    }

    // 资源目录存放指引：https://developer.android.google.cn/guide/topics/resources/providing-resources
    defaultConfig {

        // 无痛修改包名：https://www.jianshu.com/p/17327e191d2e
        applicationId 'run.yigou.gxzy'

        // 仅保留中文语种的资源
        resConfigs 'zh'

        // 仅保留 xxhdpi 图片资源（目前主流分辨率 1920 * 1080）
        resConfigs 'xxhdpi'

        // 混淆配置
        proguardFiles 'proguard-sdk.pro', 'proguard-app.pro'

        // 启用 MultiDex 支持（解决方法数超过 64K 限制）
        multiDexEnabled true

        // 日志打印开关
        buildConfigField('boolean', 'LOG_ENABLE', '' + LOG_ENABLE + '')
        // 测试包下的 BuglyId
        buildConfigField('String', 'BUGLY_ID', '"' + BUGLY_ID + '"')
        // 测试服务器的主机地址
        buildConfigField('String', 'HOST_URL', '"' + HOST_URL + '"')
    }

    // Apk 签名的那些事：https://www.jianshu.com/p/a1f8e5896aa2
    signingConfigs {
        config {
            storeFile file('../jks/ctwhzy.jks')
            storePassword 'andyxiaoyang1A'
            keyAlias 'ctwhzy'
            keyPassword 'andyxiaoyang1A'
        }
    }

    // 构建配置：https://developer.android.google.cn/studio/build/build-variants
    buildTypes {

        debug {
            // 给包名添加后缀
            applicationIdSuffix '.debug'
            // 调试模式开关
            debuggable true
            jniDebuggable true
            // 移除无用的资源
            shrinkResources false
            // 代码混淆开关
            minifyEnabled false
            // 签名信息配置
            signingConfig signingConfigs.config
            // 添加清单占位符
            addManifestPlaceholders([
                    'app_name': '@string/app_name_debug'
            ])
            // 调试模式支持32位和64位架构
            ndk {
                abiFilters 'armeabi-v7a', 'arm64-v8a'
            }
        }

        preview.initWith(debug)
        preview {
            applicationIdSuffix '.preview'
            // 代码混淆开关
            //minifyEnabled true
            // 添加清单占位符
            addManifestPlaceholders([
                    'app_name': '@string/app_name_preview'
            ])
            // Preview版本支持32位和64位架构
            ndk {
                abiFilters 'armeabi-v7a', 'arm64-v8a'
            }
        }

        release {
            // 调试模式开关
            debuggable false
            jniDebuggable false
            // 移除无用的资源
            shrinkResources true
            // 代码混淆开关
            minifyEnabled true
            // 签名信息配置
            signingConfig signingConfigs.config
            // 添加清单占位符
            addManifestPlaceholders([
                    'app_name': '@string/app_name'
            ])
            // 仅保留两种架构的 so 库，根据 Bugly 统计得出
            ndk {
                // armeabi：万金油架构平台（占用率：0%）
                // armeabi-v7a：曾经主流的架构平台（占用率：10%）
                // arm64-v8a：目前主流架构平台（占用率：95%）
                abiFilters 'armeabi-v7a', 'arm64-v8a'
            }
        }
    }
    sourceSets.main {
        jni.srcDirs = []
        jniLibs.srcDir "libs/"
    }

    packaging {
        // 剔除这个包下的所有文件（不会移除签名信息）
        resources {
            excludes += ['META-INF/*******']
        }
    }

    // 注意：已从 AspectJX 迁移到 freefair.aspectj，无需额外配置

    applicationVariants.configureEach { variant ->
        // apk 输出文件名配置
        variant.outputs.configureEach { output ->
            outputFileName = rootProject.getName() + '_v' + variant.versionName + '_' + variant.buildType.name
            if (variant.buildType.name == buildTypes.release.getName()) {
                outputFileName += '_' + new Date().format('MMdd')
            }
            outputFileName += '.apk'
        }
    }
}

dependencies {
    // MultiDex 支持（解决方法数超过 64K 限制）
    implementation libs.multidex

    // 基类封装
    implementation project(':library:base')
    // 控件封装
    implementation project(':library:widget')
    // 友盟封装
    implementation project(':library:umeng')
    // 轻量事件框架：https://github.com/LucasXu01/XBus
    implementation project(':library:xbus')

    // Android平台通用的RSA算法工具类及其例子 https://github.com/gzu-liyujiang/RSAUtils
    implementation libs.rsaUtils

    // 添加Bouncy Castle库以支持国密SM2算法
    implementation libs.bouncyCastle

    // AndroidUtilCode 是一个强大易用的安卓工具类库
    implementation libs.utilCodex

    // Markwon dependencies for Markdown rendering
    implementation libs.markwonCore
    implementation libs.markwonHtml
    implementation libs.markwonImage
    implementation libs.markwonLinkify
    implementation libs.markwonStrikethrough
    implementation libs.markwonTables
    implementation libs.markwonTasklist

    // 权限请求框架：https://github.com/getActivity/XXPermissions
    implementation libs.xxPermissions

    // 标题栏框架：https://github.com/getActivity/TitleBar
    implementation libs.titleBar

    // 吐司框架：https://github.com/getActivity/Toaster
    implementation libs.toaster

    // 网络请求框架：https://github.com/getActivity/EasyHttp
    implementation libs.easyHttp
    // OkHttp 框架：https://github.com/square/okhttp
    implementation libs.okHttp
    // 添加 SSE 扩展库
    implementation libs.okHttpSse

    // Json 解析框架：https://github.com/google/gson
    implementation libs.gson
    // Gson 解析容错：https://github.com/getActivity/GsonFactory
    implementation libs.gsonFactory

    implementation libs.shapeView

    // AOP 插件库：https://mvnrepository.com/artifact/org.aspectj/aspectjrt
    implementation libs.aspectjrt

    // 图片加载框架：https://github.com/bumptech/glide
    // 官方使用文档：https://github.com/Muyangmin/glide-docs-cn
    implementation libs.glide
    annotationProcessor libs.glideCompiler

    implementation libs.groupedRecyclerViewAdapter

    // 沉浸式框架：https://github.com/gyf-dev/ImmersionBar
    implementation libs.immersionBar

    // 手势 ImageView：https://github.com/Baseflow/PhotoView
    implementation libs.photoView

    // Bugly 异常捕捉：https://bugly.qq.com/docs/user-guide/instruction-manual-android/?v=20190418140644
    implementation libs.bugly
    implementation libs.buglyNative

    // 动画解析库：https://github.com/airbnb/lottie-android
    // 动画资源：https://lottiefiles.com、https://icons8.com/animated-icons
    implementation libs.lottie

    // 上拉刷新下拉加载框架：https://github.com/scwang90/SmartRefreshLayout
    implementation libs.refreshLayoutKernel
    implementation libs.refreshHeaderMaterial

    // 日志打印框架：https://github.com/JakeWharton/timber
    implementation libs.timber

    // 指示器框架：https://github.com/ongakuer/CircleIndicator
    implementation libs.circleIndicator

    // 腾讯 MMKV：https://github.com/Tencent/MMKV
    implementation libs.mmkvStatic

    // 内存泄漏监测框架：https://github.com/square/leakcanary
    debugImplementation libs.leakCanary
    previewImplementation libs.leakCanary

    // GreenDAO 数据库
    implementation libs.greenDao

    // 多语种：https://github.com/getActivity/MultiLanguages
    // 悬浮窗：https://github.com/getActivity/XToast
    // 日志输出：https://github.com/getActivity/Logcat
    // 工具类：https://github.com/Blankj/AndroidUtilCode
    // 轮播图：https://github.com/bingoogolapple/BGABanner-Android
    // 二维码：https://github.com/bingoogolapple/BGAQRCode-Android
    // 跑马灯：https://github.com/sunfusheng/MarqueeView
    // 对象注解：https://www.jianshu.com/p/f1f888e4a35f
    // 对象存储：https://github.com/leavesC/DoKV
    // 多渠道打包：https://github.com/Meituan-Dianping/walle
    // 设备唯一标识：http://msa-alliance.cn/col.jsp?id=120
    // 嵌套滚动容器：https://github.com/donkingliang/ConsecutiveScroller
    // 隐私调用监控：https://github.com/huage2580/PermissionMonitor
}

greendao {
    schemaVersion 1
    daoPackage 'run.yigou.gxzy.greendao.gen'
    targetGenDir 'src/main/java'
}
